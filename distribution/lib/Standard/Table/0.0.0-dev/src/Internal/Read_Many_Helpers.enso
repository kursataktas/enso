private

from Standard.Base import all
import Standard.Base.Data.Read.Many_Files_List.Many_Files_List
import Standard.Base.Errors.Illegal_Argument.Illegal_Argument

import project.Column.Column
import project.Return_As_Table.Return_As_Table
import project.Table.Table

find_files_list_in_table (that : Table) -> Many_Files_List =
    if that.column_count == 1 then Many_Files_List.Value that (that . at 0 . to_vector) else
        path_columns = that.select_columns "path" case_sensitivity=..Insensitive on_problems=..Report_Error
        not_found = path_columns.is_error || (path_columns.column_count == 0)
        if not_found then Error.throw (Illegal_Argument.Error "To use a Table as a list of files to read, it either must consist of just a single column or contain a column called 'paths' (case insensitive).") else
            if path_columns.column_count > 1 then Error.throw (Illegal_Argument.Error "Multiple 'paths' column candidates found: "+path_columns.column_names.to_display_text+".") else
                path_column_name = path_columns.at 0 . name
                Many_Files_List.Value that (that.at path_column_name . to_vector)

make_return (return_shape : Return_As_Table) (input : Many_Files_List) (objects : Vector Any) -> Table =
    r = _merge_input_and_objects input objects
    base_table = r.first
    objects_column_name = r.second
    case return_shape of
        Return_As_Table.Table_Of_Objects ->
            base_table
        Return_As_Table.Merged_Table ->
            _expand_objects base_table objects_column_name

_merge_input_and_objects (input : Many_Files_List) (objects : Vector Any) -> Pair Table Text =
    base_table = case input.original_value of
        table : Table -> table
        column : Column -> column.to_table
        ## Fallback - any unknown input shape is treated the same as
           Vector input - we just extract the list of files from it
        _ ->
            Table.new [["Path", input.paths_to_load]]

    unique_naming = base_table.column_naming_helper.create_unique_name_strategy
    unique_naming.mark_used base_table.column_names
    objects_column_name = unique_naming.make_unique "Value"
    base_table.set (Column.from_vector objects_column_name objects) as=objects_column_name set_mode=..Add

_expand_objects (table : Table) (objects_column_name:Text) -> Text =
    table
        . expand_to_rows objects_column_name at_least_one_row=True
        . expand_column objects_column_name prefix=..None
